#!/usr/bin/env perl
use Mojolicious::Lite -signatures;

# In-memory "database"
helper data => sub { state $data = {} };

# Token generator helper
helper generate_token => sub ($self) {

    # Generate token with 6 characters
    my @chars = ('A'..'Z', 1 .. 9);
    my $token = join '' => map $chars[rand @chars] => 1 .. 6;

    # Done, if doesn't exist
    return $token unless exists $self->data->{$token};
    return $self->generate_token;
};

# First steps: static pages only
get '/' => 'choose_role';
get '/start_session';
get '/join_session';

# Create a session and generate a token for session members
post '/start_session' => sub ($c) {

    # Initiate
    my $token = $c->generate_token;
    $c->data->{$token} = {
        name        => $c->param('name') // 'Anonymous session',
        persons     => [],
        speaker_ids => [],
        token       => $token,
    };

    # Store and work with it
    $c->session(token => $token);
    $c->redirect_to('chair');
};

# TODO debug only
get '/session_list' => sub ($c) {
    my @sessions = sort {$a->{name} cmp $b->{name}} values %{$c->data};
    $c->render(format => 'txt', text =>
        join "\n" => map "$_->{name}: $_->{token}\n" => @sessions
    );
};

# TODO debug only
get '/dump' => sub ($c) {
    $c->render(format => 'txt', text => $c->dumper($c->data));
};

app->start;
__DATA__

@@ choose_role.html.ep
% layout 'default';
% title 'Redeliste';
<p>
    %= link_to 'Start a new session'        => 'start_session'
    %= link_to 'Join an existing session'   => 'join_session'
</p>

@@ start_session.html.ep
% layout 'default';
% title 'Start a new session';
%= form_for start_session => (method => 'post') => begin
    <p>
        %= label_for 'name' => 'Name:'
        %= text_field name => '', id => 'name'
    </p>
    <p><%= submit_button 'Start session' %></p>
% end

@@ layouts/default.html.ep
<!DOCTYPE html><html><head><title><%= title %></title></head><body>
%= content
</body></html>
