#!/usr/bin/env perl
use Mojolicious::Lite -signatures;

# Load configuration from redeliste.conf
plugin 'Config';
app->secrets([app->config('secret')]);

# In-memory "database"
helper data => sub { state $data = app->config('init_sessions') // {} };

# Client connections (SESSION_TOKEN => [PERSON_ID => TX])
helper clients => sub { state $clients = {} };

# Full state dump for clients
helper state_dump => sub ($c) {{
    session => $c->stash('session_data'),
    userId  => $c->session('user_id'),
    wsURL   => $c->url_for('sync')->to_abs->to_string,
}};

# Token generator helper
helper generate_token => sub ($self) {

    # Generate token with 6 characters
    my @chars = ('A'..'Z', 1 .. 9);
    my $token = join '' => map $chars[rand @chars] => 1 .. 6;

    # Done, if doesn't exist
    return $token unless exists $self->data->{$token};
    return $self->generate_token;
};

# Add someone to a speaker list
helper request_to_speak => sub ($self, $id) {
    my $token = $self->session('token');
    $self->app->log->debug("Request to speak: $token - $id");
    # TODO
};

# Send state to all clients for the given session
helper broadcast => sub ($self) {
    my $clients = $self->clients->{$self->session('token')};
    $_->send({json => $self->state_dump}) for grep {defined} @$clients;
};

# First steps: static forms only
get '/' => 'choose';
get '/start';
get '/join';

# Create a session and generate a token for session members
post '/start' => sub ($c) {

    # Prepare data
    my $name    = $c->param('name')  //  'Anonymous session';
    my $uname   = $c->param('uname') // ('Anonymous' . int(rand 10_000));
    my $star    = $c->param('sex') eq 'male' ? '' : 1;

    # Initiate
    my $token = $c->generate_token;
    $c->data->{$token} = {
        name        => $name,
        persons     => [{
            id      => 0,
            name    => $uname,
            active  => 0,
            spoken  => 0,
            star    => $star,
        }],
        speaker_ids => [],
        token       => $token,
    };

    # Store and work with it
    $c->session({token => $token, user_id => 0, role => 'chair'});
    $c->redirect_to('host');
};

# Host a session
get '/host' => sub ($c) {
    $c->reply->not_found unless $c->session('role') eq 'chair';
};

# Join a session
post '/join' => sub ($c) {

    # Prepare data
    my $name = $c->param('name') // ('Anonymous' . int(rand 10_000));
    my $star = $c->param('sex') eq 'male' ? '' : 1;

    # Lookup session
    my $token   = $c->param('token');
    my $session = $c->data->{$token};
    return $c->reply->not_found unless defined $session;

    # Inject data
    my $persons = $session->{persons};
    my $plen    = scalar @$persons;
    my $person  = {
        id      => $plen,
        name    => $name,
        active  => 0,
        spoken  => 0,
        star    => $star,
    };
    push @$persons, $person;

    # Store and work with it
    $c->session({token => $token, user_id => $person->{id}, role => 'user'});
    $c->redirect_to('attend');
};

# A session has been joined, retrieve its data
under sub ($c) {

    # Session found?
    my $session = $c->data->{$c->session('token') // ''};
    $c->reply->not_found and return unless defined $session;

    # Si!
    $c->stash(session_data => $session);
    return 1;
};

# Attend a session
get '/attend';

# Read state data dump as JSON
get '/data' => [format => 'json'] => sub ($c) {
    $c->render(json => $c->state_dump);
};

# Read state data dump as text
get '/data' => [format => 'txt'] => sub ($c) {
	$c->render(text => $c->dumper($c->state_dump));
};

# Push synchronization
websocket '/sync' => sub ($c) {

    # Keep connection alive for at least one day
    $c->inactivity_timeout(app->config('timeout'));

    # Lookup corresponding data
    my $session = $c->stash('session_data');
    my $person  = $session->{persons}[$c->session('user_id')];
    return $c->reply->not_found unless $person;

    # Store connection (data structure is auto-vivified)
    $c->clients->{$session->{token}}[$person->{id}] = $c->tx;
    $person->{active} = 1;

    # React on a request to speak (message is irrelevant)
    $c->on(message => sub {
        $c->request_to_speak($session->{token}, $person->{id});
        $c->broadcast;
    });

    # React on a client leaving
    $c->on(finish => sub {
        $c->clients->{$session->{token}}[$person->{id}] = undef;
        $person->{active} = 0;
        $c->broadcast;
    });

    # Send updated session data to all clients
    $c->broadcast;
};

# Reset local storage
get '/reset' => sub ($c) {
    delete $c->session->{$_} for qw(token user_id role);
    $c->render(text => 'OK');
};

app->start;
