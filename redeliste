#!/usr/bin/env perl
use Mojolicious::Lite -signatures;

# In-memory "database"
# TODO remove test data
helper data => sub { state $data = {
    "FOOBAR" => {
      "name" => "Sitzung",
      "persons" => [
        {
          "id" => 0,
          "name" => "Mirko",
          "spoken" => "",
          "star" => ""
        },
        {
          "id" => 1,
          "name" => "Yasemin",
          "spoken" => "",
          "star" => 1
        }
      ],
      "speaker_ids" => [],
      "token" => "FOOBAR"
    }
} };

# Token generator helper
helper generate_token => sub ($self) {

    # Generate token with 6 characters
    my @chars = ('A'..'Z', 1 .. 9);
    my $token = join '' => map $chars[rand @chars] => 1 .. 6;

    # Done, if doesn't exist
    return $token unless exists $self->data->{$token};
    return $self->generate_token;
};

# First steps: static forms only
get '/' => 'choose';
get '/start';
get '/join';

# Create a session and generate a token for session members
post '/start' => sub ($c) {

    # Prepare data
    my $name    = $c->param('name')  //  'Anonymous session';
    my $uname   = $c->param('uname') // ('Anonymous' . int(rand 10_000));
    my $star    = $c->param('sex') eq 'male' ? '' : 1;

    # Initiate
    my $token = $c->generate_token;
    $c->data->{$token} = {
        name        => $name,
        persons     => [{id => 0, name => $uname, spoken => '', star => $star}],
        speaker_ids => [],
        token       => $token,
    };

    # Store and work with it
    $c->session({token => $token, user_id => 0, role => 'chair'});
    $c->redirect_to('host');
};

# Host a session
get '/host' => sub ($c) {
    $c->reply->not_found unless defined $c->data->{$c->session('token')};
    $c->reply->not_found unless $c->session('role') eq 'chair';
};

# Join a session
post '/join' => sub ($c) {

    # Prepare data
    my $name = $c->param('name') // ('Anonymous' . int(rand 10_000));
    my $star = $c->param('sex') eq 'male' ? '' : 1;

    # Lookup session
    my $token   = $c->param('token');
    my $session = $c->data->{$token};
    return $c->reply->not_found unless defined $session;

    # Inject data
    my $persons = $session->{persons};
    my $plen    = scalar @$persons;
    my $person  = {id => $plen, name => $name, spoken => '', star => $star};
    push @$persons, $person;

    # Store and work with it
    $c->session({token => $token, user_id => $person->{id}, role => 'user'});
    $c->redirect_to('attend');
};

# Attend a session
get '/attend' => sub ($c) {
    $c->reply->not_found unless defined $c->data->{$c->session('token')};
};

# Read all session data
get '/data' => [format => 'json'] => sub ($c) {

    # Lookup session
    my $session = $c->data->{$c->session('token')};
    return $c->reply->not_found unless defined $session;

    # Send as JSON
    $c->render(json => {
        session => $session,
        userId  => $c->session('user_id'),
    });
};

# Reset local storage
get '/reset' => sub ($c) {
    delete $c->session->{$_} for qw(token user_id role);
    $c->render(text => 'OK');
};

# TODO debug only
get '/dump' => sub ($c) {
    $c->render(format => 'txt', text => $c->dumper([$c->data, $c->session]));
};

app->start;
